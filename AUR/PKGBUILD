# Maintainer: Supdrewin <supdrewin at outlook dot com>

# shellcheck disable=SC2034,SC2148,SC2154

pkgname=rgb-cooling-hat-git
pkgver=r80.0f1ebc5
pkgrel=1
pkgdesc="RGB Cooling HAT advanced utility for YB-EPV02 and it's SSD1306"
arch=('any')
url=https://github.com/supdrewin/rgb-cooling-hat
license=('MPL2')
makedepends=('cargo' 'git')
provides=('rgb-cooling-hat')
conflicts=('rgb-cooling-hat')
install=${pkgname/-git/}.install
source=("$pkgname::git+$url")
md5sums=('SKIP')

pkgver() {
    cd "$pkgname" && (
        set -o pipefail

        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' || {
            printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
        }
    )
}

build() {
    cd "$pkgname" && make
}

package() {
    export PACKAGE_ENV=1

    cd "$pkgname" && {
        make DIST="$pkgdir" install && {
            ln -fs "/usr/lib/rgb-cooling-hat/main.sh" \
                "$pkgdir/usr/bin/rgb-cooling-hat"
        }
    }

    unset PACKAGE_ENV
}
